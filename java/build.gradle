plugins {
    id 'java'
    id 'maven-publish'
}

group = 'dev.babbaj'
version = '1.0'

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

repositories {
    mavenCentral()
}

dependencies {
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = 'dev.babbaj'
            artifactId = 'nether-pathfinder'
            from components.java
        }
    }

    repositories {
        maven {
            name = "GithubPackages"
            url = new URI("https://maven.pkg.github.com/Babbaj/nether-pathfinder")
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
}

private static java.nio.file.Path getResourceOutput(SourceSet sourceSet) {
    return sourceSet.getOutput().getResourcesDir().toPath();
}

final String compileLibraryOutput = 'compilePathfinderLibrary';

task buildNatives(type: Exec) {
    final File outputDir = new File(project.getBuildDir(), compileLibraryOutput)
    outputDir.mkdirs()

    workingDir outputDir
    commandLine new File(getProjectDir(), "multiplat_build.sh").absolutePath.toString(), getProjectDir().absolutePath.toString()
}


task moveLibraryToResources(type: Copy) {
    dependsOn buildNatives

    from (new File(project.getBuildDir(), compileLibraryOutput)) {
        include '*.dll'
        include '*.so'
        include '*.dylib'
    }
    into getResourceOutput(sourceSets.main).resolve('native')
}

jar.dependsOn(moveLibraryToResources)
